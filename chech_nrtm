#!/usr/bin/env bb

;; babashka install instructions
;; https://github.com/babashka/babashka#installation

;; Example query results
;; $ echo '!JRADB' | nc nrtm.radb.net 43
;; A526
;; {
;;     "RADB": {
;;         "source_type": "regular",
;;         "authoritative": false,
;;         "object_class_filter": null,
;;         "rpki_rov_filter": false,
;;         "scopefilter_enabled": false,
;;         "route_preference": null,
;;         "local_journal_kept": true,
;;         "serial_oldest_journal": 4554272,
;;         "serial_newest_journal": 4632467,
;;         "serial_last_export": null,
;;         "serial_newest_mirror": 4632467,
;;         "last_update": "2026-02-21T13:59:07.056912+00:00",
;;         "synchronised_serials": true
;;     }
;; }
;; C

(require '[babashka.cli :as cli])
(require '[cheshire.core :as json])
(require '[clojure.pprint :refer [cl-format]])
(import '[java.net Socket])

(def cli-spec 
    { :spec
        { :primary {
                :desc "Primary NRTM hostname"
                :default "nrtm.radb.net"
                :required true}
        :mirror {
                :desc "Mirror NRTM hostname"
                :default "whois.radb.net"
                :required true}
        :source {
                :desc "Source database to compare (e.g. RADB)"
                :default "RADB"
                :required true}
        :warning {
                :desc "Amount of drift for warning state [defualt 30]"
                :default 30
                :required true}
        :critical {
                :desc "Amount of drift for critical state [default 50]"
                :default 50
                :required true
                }}})

(defn show-help
  [spec]
  (cli/format-opts (merge spec {:order (vec (keys (:spec spec)))})))

(defn query-whois!
  "Using the `!J<SOURCE>` query get the current NRTM status."
  [host source]
    (with-open [sock (Socket. host 43)
        writer (io/writer sock)
        reader (io/reader sock)]
        (.write writer (str "!J" source "\r\n"))
        (.flush writer)
        (doall (line-seq reader))))

(defn get-nrtm-status
  "Parse the response from whois into a json map."
  [host source]
  (let [response (query-whois! host source)
        result (drop-last 1 (drop 1 response))
        status (json/parse-string (apply str result) true)]
  ((keyword source) status)))

(defn get-current-serial
  ""
  [host source]
  (let [status (get-nrtm-status host source)]
    (:serial_newest_mirror status)))

(defn drift
  ""
  [primary mirror source]
  (let [primary-serial (get-current-serial primary source)
        mirror-serial (get-current-serial mirror source)]
    (- primary-serial mirror-serial)))

(defn check-state
  ""
  [warning critical drift]
  (cond
    (>= drift critical)
    (do
      (println (cl-format nil "NRTM CRITICAL - Mirror out of sync [~d/~d]" drift critical))
      (System/exit 2))
    (>= drift warning)
    (do
      (println (cl-format nil "NRTM WARNING - Mirror delayed [~d/~d]" drift warning))
      (System/exit 1))
    (< drift 0)
    (do
      (println (cl-format nil "NRTM UNKNOWN - Mirror state unknown [~d/~d]" drift 0))
      (System/exit 3)))
  (println "NRTM OK - Mirror in sync")
  (System/exit 0))

(defn -main
  [args]
  (let [opts (cli/parse-opts args cli-spec)]
    (if (or (:help opts) (:h opts))
      (println (show-help cli-spec))
      (let [drift (drift (:primary opts) (:mirror opts) (:source opts))]
        (check-state (:warning opts) (:critical opts) drift)))))

(-main *command-line-args*)
